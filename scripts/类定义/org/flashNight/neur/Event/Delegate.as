/*
# org.flashNight.neur.Event.Delegate 类使用指南

## 1. 介绍

`org.flashNight.neur.Event.Delegate` 类是一种高效的动态参数传递优化工具，专为需要频繁传递动态参数的场景设计。它通过**预处理参数**与**缓存机制**，显著减少 `apply` 方法的性能开销，特别适合在事件驱动和回调机制中使用。该类旨在帮助开发者在处理动态参数传递时保持高效的性能，避免不必要的性能损失。

## 2. 功能概述

- **委托创建与缓存**：为每个函数和作用域分配唯一标识符，将生成的委托函数缓存，避免重复创建相同的函数，减少内存占用。
- **动态参数预处理**：委托函数支持动态传参，并通过预处理与缓存机制优化 `apply` 调用，特别适合频繁调用的场景。
- **作用域绑定**：允许为任意函数指定作用域，使函数在调用时能正确引用 `this`，确保上下文不丢失。
- **性能优化**：针对参数数量的不同，选择最优的函数调用方式，减少运行时判断和开销。

## 3. 使用方法

### 3.1 `create(scope:Object, method:Function):Function`

创建一个委托函数，将指定的方法绑定到给定的作用域。通过缓存机制优化委托函数的创建和复用，提升性能。

- `scope`：作为 `this` 绑定的对象。如果为 `null`，函数将在全局作用域执行。
- `method`：需要在该作用域内执行的函数，必须为有效的函数引用，不能为 `null` 或 `undefined`。

**示例**：
```as
var delegate = org.flashNight.neur.Event.Delegate.create(testInstance, testInstance.sayHello);
trace(delegate("Hello")); // 输出: Hello, my name is Alice
```

### 3.2 `createWithParams(scope:Object, method:Function, params:Array):Function`

创建一个带有预定义参数的委托函数，将函数和预定义的参数绑定到指定的作用域。该方法通过将参数封装在闭包中，避免了每次调用时使用 `apply`，从而优化了动态传参场景下的性能。

- `scope`：作为 `this` 绑定的对象。
- `method`：需要在该作用域内执行的函数。
- `params`：预定义的参数数组。

**示例**：
```as
var preBoundDelegate = org.flashNight.neur.Event.Delegate.createWithParams(null, preBoundTest, ["foo", "bar"]);
trace(preBoundDelegate()); // 输出: Pre-bound args: foo and bar
```

### 3.3 `clearCache():Void`

清理缓存中的所有委托函数。建议在大型应用程序或长时间运行的程序中定期调用此方法，以防止内存泄漏。

**示例**：
```as
org.flashNight.neur.Event.Delegate.clearCache();
```

## 4. 性能优化重点

### 4.1 动态参数传递的预处理与优化

**`Delegate` 类的核心优势在于其动态参数的预处理能力**，通过 `createWithParams` 方法，预先将动态参数绑定至委托函数，避免了每次调用时重新解析参数和调用 `apply`。相比于传统的基于 `apply` 的动态参数传递，这种方法大大减少了性能开销。

- **预处理机制**：在创建委托函数时即完成参数绑定，使用直接调用 (`call`) 取代 `apply`，确保性能最优。
- **缓存机制**：通过缓存相同作用域和函数的委托，避免每次创建委托函数时重复解析参数。

**示例**：
```as
function dynamicArgumentTest() {
    return Array.prototype.join.call(arguments, ", ");
}

var dynamicDelegate = org.flashNight.neur.Event.Delegate.create(null, dynamicArgumentTest);
trace(dynamicDelegate(1, "a", true, null)); // 输出: 1, a, true, null
```

在该示例中，`Delegate` 类会根据传入参数的数量选择最优的调用方式，避免每次使用 `apply` 带来的性能开销。

### 4.2 预绑定参数的优势

使用 `createWithParams` 方法时，参数会在创建时被绑定到委托函数中，避免了每次任务执行时进行参数解析的过程。特别是在高频任务调用中，提前绑定参数的方式能有效降低函数调用的成本。

**示例**：
```as
function preBoundTest(arg1, arg2) {
    return "Pre-bound args: " + arg1 + " and " + arg2;
}

var preBoundDelegate = org.flashNight.neur.Event.Delegate.createWithParams(null, preBoundTest, ["foo", "bar"]);
trace(preBoundDelegate()); // 输出: Pre-bound args: foo and bar
```

相比动态传参在每次执行时都需要 `apply` 进行处理，预绑定参数的方式更加高效，避免了不必要的参数解析和性能损耗。

### 4.3 缓存机制的高效利用

`Delegate` 类通过缓存相同作用域和函数组合生成的委托函数，避免了每次调用时重复创建新函数。在频繁调用相同任务的场景下，缓存机制可以大幅减少内存占用和函数创建的开销。

**示例**：
```as
var delegateA1 = org.flashNight.neur.Event.Delegate.create(null, globalTestFunction);
var delegateA2 = org.flashNight.neur.Event.Delegate.create(null, globalTestFunction);
trace(delegateA1 === delegateA2); // 输出: true
```

## 5. 性能优化建议

### 5.1 动态参数传递场景

在处理大量动态参数时，`Delegate` 的预处理机制可以极大减少运行时的开销，尤其在频繁调用的事件处理和回调场景中。建议优先使用 `createWithParams` 方法预先绑定参数，避免频繁使用 `apply` 带来的性能损耗。

### 5.2 缓存复用

尽可能复用已存在的委托函数，避免频繁创建新的委托。通过 `Delegate` 类的缓存机制，可以有效减少对象创建的开销，尤其在高频调用的场景中表现尤为显著。

### 5.3 定期清理缓存

在长时间运行的程序中，缓存中的委托函数会持续占用内存。建议在不再需要这些委托函数时调用 `clearCache()`，清理缓存以释放内存资源，避免内存泄漏。

## 6. 常见使用场景

### 6.1 事件处理与回调机制

在需要频繁注册和注销事件处理函数的场景中，`Delegate` 通过缓存机制复用委托函数，并结合预处理动态参数的方式，确保回调函数在高频调用中保持高效。

### 6.2 动态参数传递

`Delegate` 类的动态参数传递优化功能尤其适用于需要根据不同条件传递不同参数的场景。预处理与缓存机制的结合大幅优化了任务调度和事件处理中的函数调用性能。

## 7. 注意事项

1. **参数预处理与缓存**：当相同的作用域和方法组合多次被调用时，`Delegate` 类会自动复用缓存的委托函数，避免重复创建新函数。确保参数是稳定的，以充分利用缓存机制。
2. **性能敏感场景**：对于需要频繁传递动态参数的高性能场景，优先考虑使用 `createWithParams` 进行预处理，减少 `apply` 带来的开销。
3. **定期清理缓存**：在长时间运行的程序中，缓存的委托函数会持续占用内存资源，建议在合适的时间调用 `clearCache()` 释放内存。

---

通过合理使用 `org.flashNight.neur.Event.Delegate` 类，可以极大提升动态参数传递和任务调度的性能。特别是在事件驱动和回调机制中，`Delegate` 的预处理和缓存机制能有效减少内存占用和函数调用开销，帮助团队在开发高效的任务调度系统时取得显著的性能提升。

*/
import org.flashNight.gesh.object.ObjectUtil;
import org.flashNight.naki.DataStructures.Dictionary;

class org.flashNight.neur.Event.Delegate {
    private static var cacheCreate:Dictionary; // 不带预定义参数的委托函数缓存
    private static var cacheCreateWithParams:Dictionary; // 带预定义参数的委托函数缓存
    
    private static var GLOBAL_SCOPE:Object = {}; // 使用唯一对象表示全局作用域
    private static var isInitialized:Boolean = false; // 标志位，确保初始化只进行一次
    
    /**
     * 初始化缓存字典。需要在使用 Delegate 类之前调用此方法。
     */
    public static function init():Void {
        if (!isInitialized) {
            cacheCreate = new Dictionary();
            cacheCreateWithParams = new Dictionary();
            isInitialized = true;
        }
    }

    /**
     * 创建一个委托函数，将指定方法绑定到给定的作用域。
     * 利用缓存机制优化委托函数的创建，避免重复创建相同的委托函数。
     * 
     * @param scope  将作为 `this` 绑定的对象。如果为 `null`，则方法将在全局作用域中执行。
     * @param method 需要在该作用域内执行的函数。必须为有效的函数引用，不能为 `null` 或 `undefined`。
     * @return 返回一个新函数，可以带参数调用，并在指定的作用域内执行。若相同的 `scope` 和 `method` 已存在，
     *         则返回缓存中的委托函数，避免重复创建。
     */
    public static function create(scope:Object, method:Function):Function {
        // 检查 method 是否有效
        if (method == null) {
            throw new Error("The provided method is undefined or null");
        }

        // 将 cacheCreate 局部化，减少多次全局访问的成本
        var cache:Dictionary = cacheCreate;

        var cacheKey:String;
        var wrappedFunction:Function;

        // 如果 scope 为 null, 则使用全局作用域
        if (scope == null) {
            cacheKey = String((cache.getUID(GLOBAL_SCOPE) << 16) | cache.getUID(method));

            // 尝试从缓存中获取委托函数
            var cachedFunction:Function = cache.getItem(cacheKey);
            if (cachedFunction !== null && cachedFunction !== undefined) {
                return cachedFunction;
            }

            // 定义包装函数（无作用域）
            wrappedFunction = function() {
                var len:Number = arguments.length;
                if (len == 0) return method();
                else if (len == 1) return method(arguments[0]);
                else if (len == 2) return method(arguments[0], arguments[1]);
                else if (len == 3) return method(arguments[0], arguments[1], arguments[2]);
                else if (len == 4) return method(arguments[0], arguments[1], arguments[2], arguments[3]);
                else if (len == 5) return method(arguments[0], arguments[1], arguments[2], arguments[3], arguments[4]);
                else return method.apply(null, arguments); // 超过5个参数时，使用 apply 调用
            };
        } else {
            // 否则使用指定的 scope
            cacheKey = String((cache.getUID(scope) << 16) | cache.getUID(method));

            // 尝试从缓存中获取委托函数
            var cachedFunction:Function = cache.getItem(cacheKey);
            if (cachedFunction !== null && cachedFunction !== undefined) {
                return cachedFunction;
            }

            // 定义包装函数（带作用域）
            wrappedFunction = function() {
                var len:Number = arguments.length;
                if (len == 0) return method.call(scope);
                else if (len == 1) return method.call(scope, arguments[0]);
                else if (len == 2) return method.call(scope, arguments[0], arguments[1]);
                else if (len == 3) return method.call(scope, arguments[0], arguments[1], arguments[2]);
                else if (len == 4) return method.call(scope, arguments[0], arguments[1], arguments[2], arguments[3]);
                else if (len == 5) return method.call(scope, arguments[0], arguments[1], arguments[2], arguments[3], arguments[4]);
                else return method.apply(scope, arguments); // 超过5个参数时，使用 apply 调用
            };
        }

        // 缓存并返回包装函数
        cache.setItem(cacheKey, wrappedFunction);
        return wrappedFunction;
    }



    /**
     * 创建一个带有预定义参数的委托函数。
     * 
     * @param scope 将作为 `this` 绑定的对象。
     * @param method 需要在该作用域内执行的函数。
     * @param params 预定义的参数数组。
     * @return 返回一个新函数，可以在调用时执行，并在指定的作用域内使用预定义参数。
     */
    public static function createWithParams(scope:Object, method:Function, params:Array):Function {
        // 检查 method 是否有效
        if (method == null) {
            throw new Error("The provided method is undefined or null");
        }

        // 将 cacheCreate 局部化，减少多次全局访问的成本
        var cache:Dictionary = cacheCreateWithParams;
        if(scope == null)
        {
            var cacheKey:String = String((String(cache.getUID(GLOBAL_SCOPE) << 16) | cache.getUID(method)) + params.toString());
            // 尝试从缓存中获取委托函数
            var cachedFunctionWithParams:Function = cache.getItem(cacheKey);
            if (cachedFunctionWithParams !== null && cachedFunctionWithParams !== undefined) {
                return cachedFunctionWithParams;
            }

            // 定义包装函数
            var wrappedFunctionWithParams:Function;
            wrappedFunctionWithParams = function() {
                var len:Number = params.length;
                if (len == 0) return method();
                else if (len == 1) return method(params[0]);
                else if (len == 2) return method(params[0], params[1]);
                else if (len == 3) return method(params[0], params[1], params[2]);
                else if (len == 4) return method(params[0], params[1], params[2], params[3]);
                else if (len == 5) return method(params[0], params[1], params[2], params[3], params[4]);
                else return method.apply(null, params);
            }
        }
        else
        {
            var cacheKey:String = String((String(cache.getUID(GLOBAL_SCOPE) << 16) | cache.getUID(method)) + params.toString());
            // 尝试从缓存中获取委托函数
            var cachedFunctionWithParams:Function = cache.getItem(cacheKey);
            if (cachedFunctionWithParams !== null && cachedFunctionWithParams !== undefined) {
                return cachedFunctionWithParams;
            }

            // 定义包装函数
            var wrappedFunctionWithParams:Function;
            wrappedFunctionWithParams = function() {
                var len:Number = params.length;
                if (len == 0) return method.call(scope);
                else if (len == 1) return method.call(scope, params[0]);
                else if (len == 2) return method.call(scope, params[0], params[1]);
                else if (len == 3) return method.call(scope, params[0], params[1], params[2]);
                else if (len == 4) return method.call(scope, params[0], params[1], params[2], params[3]);
                else if (len == 5) return method.call(scope, params[0], params[1], params[2], params[3], params[4]);
                else return method.apply(scope, params);
                }
        };

        // 缓存并返回包装函数
        cache.setItem(cacheKey, wrappedFunctionWithParams);
        return wrappedFunctionWithParams;
    }

    /**
     * 清理缓存中的所有委托函数。
     */
    public static function clearCache():Void {
        cacheCreate.clear();
        cacheCreateWithParams.clear();
    }
}



/*

org.flashNight.neur.Event.Delegate.init();
// 定义一个简单的类用于测试 scope 绑定
var TestClass = function(name) {
    this.name = name;
};

TestClass.prototype.sayHello = function(greeting) {
    return greeting + ", my name is " + this.name;
};

// 定义不同的函数用于测试
function globalTestFunction() {
    return "Global function called!";
}

// 实例化一个对象用于绑定 scope
var testInstance = new TestClass("Alice");

// 测试用例 1：没有参数的函数绑定到全局作用域
var globalDelegate = org.flashNight.neur.Event.Delegate.create(null, globalTestFunction);
trace(globalDelegate()); // 输出: Global function called!

// 测试用例 2：带参数的函数绑定到指定对象作用域
var helloDelegate = org.flashNight.neur.Event.Delegate.create(testInstance, testInstance.sayHello);
trace(helloDelegate("Hello")); // 输出: Hello, my name is Alice

// 测试用例 3：改变作用域后执行相同的方法
var anotherInstance = new TestClass("Bob");
var anotherHelloDelegate = org.flashNight.neur.Event.Delegate.create(anotherInstance, testInstance.sayHello);
trace(anotherHelloDelegate("Hi")); // 输出: Hi, my name is Bob

// 测试用例 4：测试超过5个参数的调用
function testMultipleArguments(arg1, arg2, arg3, arg4, arg5, arg6) {
    return [arg1, arg2, arg3, arg4, arg5, arg6].join(", ");
}

var multiArgDelegate = org.flashNight.neur.Event.Delegate.create(null, testMultipleArguments);
trace(multiArgDelegate(1, 2, 3, 4, 5, 6)); // 输出: 1, 2, 3, 4, 5, 6

// 测试用例 5：测试 null method 抛出错误
try {
    var nullDelegate = org.flashNight.neur.Event.Delegate.create(null, null);
    trace(nullDelegate());
} catch (e:Error) {
    trace("Error caught: " + e.message); // 输出: Error caught: The provided method is undefined or null
}

// 测试用例 6：测试函数动态参数传递
function dynamicArgumentTest() {
    return Array.prototype.join.call(arguments, ", ");
}

var dynamicDelegate = org.flashNight.neur.Event.Delegate.create(null, dynamicArgumentTest);
trace(dynamicDelegate(1, "a", true, null)); // 输出: 1, a, true, null

// 测试用例 7：测试绑定到全局作用域且动态传递大量参数
function largeArgumentTest() {
    return Array.prototype.join.call(arguments, ", ");
}

var largeArgDelegate = org.flashNight.neur.Event.Delegate.create(null, largeArgumentTest);
trace(largeArgDelegate(1, 2, 3, 4, 5, 6, 7, 8, 9, 10)); // 输出: 1, 2, 3, 4, 5, 6, 7, 8, 9, 10

// 测试用例 8：测试绑定到指定作用域的函数动态传参
var scopedDynamicDelegate = org.flashNight.neur.Event.Delegate.create(testInstance, function() {
    return this.name + ": " + Array.prototype.join.call(arguments, ", ");
});
trace(scopedDynamicDelegate("apple", "banana", "orange")); // 输出: Alice: apple, banana, orange

// 测试用例 9：测试边界情况 - 空参数
var noArgumentDelegate = org.flashNight.neur.Event.Delegate.create(null, function() {
    return "No arguments";
});
trace(noArgumentDelegate()); // 输出: No arguments

// 测试用例 10：测试函数传递 null 和 undefined 作为参数
var nullUndefinedDelegate = org.flashNight.neur.Event.Delegate.create(null, function(arg1, arg2) {
    return "arg1: " + arg1 + ", arg2: " + arg2;
});
trace(nullUndefinedDelegate(null, undefined)); // 输出: arg1: null, arg2: undefined

// 测试用例 11：使用作用域的包装函数调用嵌套函数，保证作用域不丢失
function nestedFunctionTest() {
    var innerFunction = function() {
        return this.name + " from inner function";
    };
    return innerFunction.call(this);
}

var nestedDelegate = org.flashNight.neur.Event.Delegate.create(testInstance, nestedFunctionTest);
trace(nestedDelegate()); // 输出: Alice from inner function

// 测试用例 12：绑定到不同作用域并动态传递对象作为参数
function objectArgumentTest(obj) {
    return obj.name + " is " + obj.age + " years old.";
}

var objectDelegate = org.flashNight.neur.Event.Delegate.create(null, objectArgumentTest);
trace(objectDelegate({name: "Charlie", age: 28})); // 输出: Charlie is 28 years old.

// 测试用例 13：多层作用域绑定传递带有函数参数的对象
var advancedObjectDelegate = org.flashNight.neur.Event.Delegate.create(testInstance, function(obj) {
    return this.name + " received: " + obj.saySomething();
});
trace(advancedObjectDelegate({ saySomething: function() { return "a message from object"; } })); 
// 输出: Alice received: a message from object

// 测试用例 14：测试传递嵌套数组作为参数
function arrayArgumentTest(arr) {
    return arr.join(", ");
}

var arrayDelegate = org.flashNight.neur.Event.Delegate.create(null, arrayArgumentTest);
trace(arrayDelegate([1, [2, 3], 4, ["nested", "array"]])); // 输出: 1, 2,3, 4, nested,array

// 测试用例 15：测试传递包含方法的对象
var methodInObjectDelegate = org.flashNight.neur.Event.Delegate.create(testInstance, function(obj) {
    return obj.method();
});

trace(methodInObjectDelegate({
    method: function() {
        return testInstance.name + " method called!";
    }
})); // 输出: Alice method called!

// 测试用例 16：作用域绑定后动态传递多个复杂类型参数
var complexParamDelegate = org.flashNight.neur.Event.Delegate.create(testInstance, function(num, arr, obj, str) {
    return this.name + " got: " + num + ", " + arr.join(", ") + ", " + obj.info + ", " + str;
});

trace(complexParamDelegate(42, [1, 2, 3], {info: "some info"}, "test string")); 
// 输出: Alice got: 42, 1, 2, 3, some info, test string

// 新增测试用例 17：使用 createWithParams 绑定函数并预先传递参数
function preBoundTest(arg1, arg2) {
    return "Pre-bound args: " + arg1 + " and " + arg2;
}

var preBoundDelegate = org.flashNight.neur.Event.Delegate.createWithParams(null, preBoundTest, ["foo", "bar"]);
trace(preBoundDelegate()); // 输出: Pre-bound args: foo and bar

// 新增测试用例 18：使用 createWithParams 绑定带作用域的函数并预先传递参数
var scopedPreBoundDelegate = org.flashNight.neur.Event.Delegate.createWithParams(testInstance, function(arg1, arg2) {
    return this.name + " received: " + arg1 + " and " + arg2;
}, ["baz", "qux"]);
trace(scopedPreBoundDelegate()); // 输出: Alice received: baz and qux

// 新增测试用例 19：使用 createWithParams 绑定带作用域的函数并预先传递超过5个参数
function manyArgsTest(a, b, c, d, e, f, g) {
    return "Args: " + a + ", " + b + ", " + c + ", " + d + ", " + e + ", " + f + ", " + g;
}

var manyArgsDelegate = org.flashNight.neur.Event.Delegate.createWithParams(null, manyArgsTest, [1, 2, 3, 4, 5, 6, 7]);
trace(manyArgsDelegate()); // 输出: Args: 1, 2, 3, 4, 5, 6, 7

// 新增测试用例 20：确保缓存机制工作正常，创建相同的委托应该返回相同的函数引用
var delegateA1 = org.flashNight.neur.Event.Delegate.create(null, globalTestFunction);
var delegateA2 = org.flashNight.neur.Event.Delegate.create(null, globalTestFunction);
trace(delegateA1 === delegateA2); // 输出: true

var delegateB1 = org.flashNight.neur.Event.Delegate.create(testInstance, testInstance.sayHello);
var delegateB2 = org.flashNight.neur.Event.Delegate.create(testInstance, testInstance.sayHello);
trace(delegateB1 === delegateB2); // 输出: true

var delegateC1 = org.flashNight.neur.Event.Delegate.createWithParams(null, preBoundTest, ["foo", "bar"]);
var delegateC2 = org.flashNight.neur.Event.Delegate.createWithParams(null, preBoundTest, ["foo", "bar"]);
trace(delegateC1 === delegateC2); // 输出: true

var delegateD1 = org.flashNight.neur.Event.Delegate.createWithParams(testInstance, function(arg1, arg2) {
    return this.name + " received: " + arg1 + " and " + arg2;
}, ["baz", "qux"]);
var delegateD2 = org.flashNight.neur.Event.Delegate.createWithParams(testInstance, function(arg1, arg2) {
    return this.name + " received: " + arg1 + " and " + arg2;
}, ["baz", "qux"]);
trace(delegateD1 === delegateD2); // 输出: false

// 新增测试用例 21：确保不同参数组合生成不同的委托函数
var delegateE1 = org.flashNight.neur.Event.Delegate.createWithParams(null, preBoundTest, ["foo", "bar"]);
var delegateE2 = org.flashNight.neur.Event.Delegate.createWithParams(null, preBoundTest, ["foo", "baz"]);
trace(delegateE1 === delegateE2); // 输出: false


*/
